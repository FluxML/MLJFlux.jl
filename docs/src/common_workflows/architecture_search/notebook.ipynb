{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Neural Architecture Search with MLJFlux"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "This demonstration is available as a Jupyter notebook or julia script\n",
    "[here](https://github.com/FluxML/MLJFlux.jl/tree/dev/docs/src/common_workflows/architecture_search)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Neural Architecture Search (NAS) is an instance of hyperparameter tuning concerned\n",
    "with tuning model hyperparameters defining the architecture itself. Although it's\n",
    "typically performed with sophisticated search algorithms for efficiency, in this example\n",
    "we will be using a simple random search."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  Activating project at `~/GoogleDrive/Julia/MLJ/MLJFlux/docs`\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using Pkg\n",
    "PKG_ENV = joinpath(@__DIR__, \"..\", \"..\", \"..\")\n",
    "Pkg.activate(PKG_ENV);\n",
    "Pkg.instantiate();"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "**This script tested using Julia 1.10**"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Basic Imports"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "stable_rng (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "using MLJ               # Has MLJFlux models\n",
    "using Flux              # For more flexibility\n",
    "using DataFrames        # To view tuning results in a table\n",
    "import Optimisers       # native Flux.jl optimisers no longer supported\n",
    "using StableRNGs        # for reproducibility across Julia versions\n",
    "\n",
    "stable_rng() = StableRNGs.StableRNG(123)"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Loading and Splitting the Data"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "(sepal_length = Float32[6.1, 7.3, 6.3, 4.8, 5.9, 7.1, 6.7, 5.4, 6.0, 6.9  …  5.0, 6.4, 5.7, 4.6, 5.5, 4.6, 5.6, 5.7, 6.0, 5.0], sepal_width = Float32[2.9, 2.9, 3.4, 3.4, 3.0, 3.0, 3.0, 3.9, 3.0, 3.1  …  3.3, 2.7, 2.5, 3.2, 2.4, 3.1, 2.8, 3.0, 2.9, 3.5], petal_length = Float32[4.7, 6.3, 5.6, 1.9, 5.1, 5.9, 5.0, 1.7, 4.8, 4.9  …  1.4, 5.3, 5.0, 1.4, 3.7, 1.5, 4.9, 4.2, 4.5, 1.6], petal_width = Float32[1.4, 1.8, 2.4, 0.2, 1.8, 2.1, 1.7, 0.4, 1.8, 1.5  …  0.2, 1.9, 2.0, 0.2, 1.0, 0.2, 2.0, 1.2, 1.5, 0.6])"
     },
     "metadata": {},
     "execution_count": 3
    }
   ],
   "cell_type": "code",
   "source": [
    "iris = load_iris() # a named-tuple of vectors\n",
    "y, X = unpack(iris, ==(:target), rng=stable_rng())\n",
    "X = fmap(column-> Float32.(column), X) # Flux prefers Float32 data"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Instantiating the model"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now let's construct our model. This follows a similar setup the one followed in the\n",
    "[Quick Start](../../index.md#Quick-Start)."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[ Info: For silent loading, specify `verbosity=0`. \n",
      "import MLJFlux ✔\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "NeuralNetworkClassifier(\n  builder = MLP(\n        hidden = (1, 1, 1), \n        σ = NNlib.relu), \n  finaliser = NNlib.softmax, \n  optimiser = Optimisers.Adam(eta=0.01, beta=(0.9, 0.999), epsilon=1.0e-8), \n  loss = Flux.Losses.crossentropy, \n  epochs = 10, \n  batch_size = 8, \n  lambda = 0.0, \n  alpha = 0.0, \n  rng = StableRNGs.LehmerRNG(state=0x000000000000000000000000000000f7), \n  optimiser_changes_trigger_retraining = false, \n  acceleration = ComputationalResources.CPU1{Nothing}(nothing), \n  embedding_dims = Dict{Symbol, Real}())"
     },
     "metadata": {},
     "execution_count": 4
    }
   ],
   "cell_type": "code",
   "source": [
    "NeuralNetworkClassifier = @load NeuralNetworkClassifier pkg = \"MLJFlux\"\n",
    "clf = NeuralNetworkClassifier(\n",
    "    builder = MLJFlux.MLP(; hidden = (1, 1, 1), σ = Flux.relu),\n",
    "    optimiser = Optimisers.ADAM(0.01),\n",
    "    batch_size = 8,\n",
    "    epochs = 10,\n",
    "    rng = stable_rng(),\n",
    ")"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Generating Network Architectures"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We know that the MLP builder takes a tuple of the form $(z_1, z_2, ..., z_k)$ to define\n",
    "a network with $k$ hidden layers and where the ith layer has $z_i$ neurons. We will\n",
    "proceed by defining a function that can generate all possible networks with a specific\n",
    "number of hidden layers, a minimum and maximum number of neurons per layer and\n",
    "increments to consider for the number of neurons."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "generate_networks (generic function with 1 method)"
     },
     "metadata": {},
     "execution_count": 5
    }
   ],
   "cell_type": "code",
   "source": [
    "function generate_networks(\n",
    "    ;min_neurons::Int,\n",
    "    max_neurons::Int,\n",
    "    neuron_step::Int,\n",
    "    num_layers::Int,\n",
    "    )\n",
    "    # Define the range of neurons\n",
    "    neuron_range = min_neurons:neuron_step:max_neurons\n",
    "\n",
    "    # Empty list to store the network configurations\n",
    "    networks = Vector{Tuple{Vararg{Int, num_layers}}}()\n",
    "\n",
    "    # Recursive helper function to generate all combinations of tuples\n",
    "    function generate_tuple(current_layers, remaining_layers)\n",
    "        if remaining_layers > 0\n",
    "            for n in neuron_range\n",
    "                # current_layers =[] then current_layers=[(min_neurons)],\n",
    "                # [(min_neurons+neuron_step)], [(min_neurons+2*neuron_step)],...\n",
    "                # for each of these we call generate_layers again which appends\n",
    "                # the n combinations for each one of them\n",
    "                generate_tuple(vcat(current_layers, [n]), remaining_layers - 1)\n",
    "            end\n",
    "        else\n",
    "            # in the base case, no more layers to \"recurse on\"\n",
    "            # and we just append the current_layers as a tuple\n",
    "            push!(networks, tuple(current_layers...))\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Generate networks for the given number of layers\n",
    "    generate_tuple([], num_layers)\n",
    "\n",
    "    return networks\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now let's generate an array of all possible neural networks with three hidden layers and\n",
    "number of neurons per layer ∈ [1,64] with a step of 4"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "5-element Vector{Tuple{Int64, Int64, Int64}}:\n (1, 1, 1)\n (1, 1, 5)\n (1, 1, 9)\n (1, 1, 13)\n (1, 1, 17)"
     },
     "metadata": {},
     "execution_count": 6
    }
   ],
   "cell_type": "code",
   "source": [
    "networks_space =\n",
    "    generate_networks(\n",
    "        min_neurons = 1,\n",
    "        max_neurons = 64,\n",
    "        neuron_step = 4,\n",
    "        num_layers = 3,\n",
    "    )\n",
    "\n",
    "networks_space[1:5]"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Wrapping the Model for Tuning"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Let's use this array to define the range of hyperparameters and pass it along with the\n",
    "model to the `TunedModel` constructor."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "r1 = range(clf, :(builder.hidden), values = networks_space)\n",
    "\n",
    "tuned_clf = TunedModel(\n",
    "    model = clf,\n",
    "    tuning = RandomSearch(),\n",
    "    resampling = CV(nfolds = 4, rng = 42),\n",
    "    range = [r1],\n",
    "    measure = cross_entropy,\n",
    "    n = 100,             # searching over 100 random samples are enough\n",
    ");"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Performing the Search"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Similar to the last workflow example, all we need now is to fit our model and the search\n",
    "will take place automatically:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "NeuralNetworkClassifier(\n  builder = MLP(\n        hidden = (13, 37, 37), \n        σ = NNlib.relu), \n  finaliser = NNlib.softmax, \n  optimiser = Optimisers.Adam(eta=0.01, beta=(0.9, 0.999), epsilon=1.0e-8), \n  loss = Flux.Losses.crossentropy, \n  epochs = 10, \n  batch_size = 8, \n  lambda = 0.0, \n  alpha = 0.0, \n  rng = StableRNGs.LehmerRNG(state=0xc6437cc1ff2bdb7d4fdb34a5c27173fb), \n  optimiser_changes_trigger_retraining = false, \n  acceleration = ComputationalResources.CPU1{Nothing}(nothing), \n  embedding_dims = Dict{Symbol, Real}())"
     },
     "metadata": {},
     "execution_count": 8
    }
   ],
   "cell_type": "code",
   "source": [
    "mach = machine(tuned_clf, X, y);\n",
    "fit!(mach, verbosity = 0);\n",
    "fitted_params(mach).best_model"
   ],
   "metadata": {},
   "execution_count": 8
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Analyzing the Search Results"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Let's analyze the search results by converting the history array to a dataframe and\n",
    "viewing it:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "\u001b[1m10×2 DataFrame\u001b[0m\n\u001b[1m Row \u001b[0m│\u001b[1m mlp                           \u001b[0m\u001b[1m measurement \u001b[0m\n\u001b[1m     \u001b[0m│\u001b[90m MLP…                          \u001b[0m\u001b[90m Float64     \u001b[0m\n─────┼────────────────────────────────────────────\n   1 │ MLP(hidden = (13, 37, 37), …)    0.0656207\n   2 │ MLP(hidden = (49, 33, 57), …)    0.0682199\n   3 │ MLP(hidden = (33, 41, 49), …)    0.0692709\n   4 │ MLP(hidden = (49, 41, 25), …)    0.0702684\n   5 │ MLP(hidden = (33, 53, 25), …)    0.0707775\n   6 │ MLP(hidden = (49, 45, 13), …)    0.0713691\n   7 │ MLP(hidden = (45, 29, 29), …)    0.0716759\n   8 │ MLP(hidden = (37, 37, 33), …)    0.0719319\n   9 │ MLP(hidden = (53, 57, 17), …)    0.07224\n  10 │ MLP(hidden = (37, 57, 17), …)    0.0726546",
      "text/html": [
       "<div><div style = \"float: left;\"><span>10×2 DataFrame</span></div><div style = \"clear: both;\"></div></div><div class = \"data-frame\" style = \"overflow-x: scroll;\"><table class = \"data-frame\" style = \"margin-bottom: 6px;\"><thead><tr class = \"columnLabelRow\"><th class = \"stubheadLabel\" style = \"font-weight: bold; text-align: right;\">Row</th><th style = \"text-align: left;\">mlp</th><th style = \"text-align: left;\">measurement</th></tr><tr class = \"columnLabelRow\"><th class = \"stubheadLabel\" style = \"font-weight: bold; text-align: right;\"></th><th title = \"MLJFlux.MLP{3}\" style = \"text-align: left;\">MLP…</th><th title = \"Float64\" style = \"text-align: left;\">Float64</th></tr></thead><tbody><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">1</td><td style = \"text-align: left;\">MLP(hidden = (13, 37, 37), …)</td><td style = \"text-align: right;\">0.0656207</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">2</td><td style = \"text-align: left;\">MLP(hidden = (49, 33, 57), …)</td><td style = \"text-align: right;\">0.0682199</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">3</td><td style = \"text-align: left;\">MLP(hidden = (33, 41, 49), …)</td><td style = \"text-align: right;\">0.0692709</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">4</td><td style = \"text-align: left;\">MLP(hidden = (49, 41, 25), …)</td><td style = \"text-align: right;\">0.0702684</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">5</td><td style = \"text-align: left;\">MLP(hidden = (33, 53, 25), …)</td><td style = \"text-align: right;\">0.0707775</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">6</td><td style = \"text-align: left;\">MLP(hidden = (49, 45, 13), …)</td><td style = \"text-align: right;\">0.0713691</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">7</td><td style = \"text-align: left;\">MLP(hidden = (45, 29, 29), …)</td><td style = \"text-align: right;\">0.0716759</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">8</td><td style = \"text-align: left;\">MLP(hidden = (37, 37, 33), …)</td><td style = \"text-align: right;\">0.0719319</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">9</td><td style = \"text-align: left;\">MLP(hidden = (53, 57, 17), …)</td><td style = \"text-align: right;\">0.07224</td></tr><tr class = \"dataRow\"><td class = \"rowLabel\" style = \"font-weight: bold; text-align: right;\">10</td><td style = \"text-align: left;\">MLP(hidden = (37, 57, 17), …)</td><td style = \"text-align: right;\">0.0726546</td></tr></tbody></table></div>"
      ]
     },
     "metadata": {},
     "execution_count": 9
    }
   ],
   "cell_type": "code",
   "source": [
    "history = report(mach).history;\n",
    "history_df = DataFrame(\n",
    "    mlp = [x[:model].builder for x in history],\n",
    "    measurement = [x[:measurement][1] for x in history],\n",
    ")\n",
    "first(sort!(history_df, [order(:measurement)]), 10)"
   ],
   "metadata": {},
   "execution_count": 9
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.10",
   "language": "julia"
  }
 },
 "nbformat": 4
}
